plugins {
	id "com.crystaelix.loom" version "1.9.0"
	id "eclipse"
	id "idea"
	id "maven-publish"
	id "net.kyori.blossom" version "[1,2)"
	id "me.modmuss50.mod-publish-plugin" version "0.8.4"
}

repositories {
	// Add repositories to retrieve artifacts from in here.
	mavenCentral()
	flatDir {
		dirs "libs"
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://nexus.gtnewhorizons.com/repository/public/"
				metadataSources {
					mavenPom()
					ignoreGradleMetadataRedirection()
				}
			}
		}
		filter {
			includeGroup "com.github.GTNewHorizons"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://jitpack.io/"
			}
		}
		filter {
			includeGroup "com.github.LegacyModdingMC.UniMixins"
		}
	}
	exclusiveContent {
		forRepository {
			maven {
				url = "https://cursemaven.com/"
			}
		}
		filter {
			includeGroup "curse.maven"
		}
	}
}

version = mod_version
group = mod_group
archivesBaseName = "${mod_name}-1.7.10"

java {
	toolchain.languageVersion = JavaLanguageVersion.of(8)
	withSourcesJar()
}

tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

loom {
	// The log4j config file contained in this template is Minecraft default logging config, and does not contain debug information.
	// Remove the following line and update the runtime log4j for Loom's default logging output.
	log4jConfigs.setFrom "log4j2.xml"

	silentMojangMappingsLicense()
	legacyForge {
		// Uncomment the following line for early mixin json configs.
		//mixinConfig "${mod_id}.mixins.json"
	}

	mixin.defaultRefmapName = "${mod_id}.refmap.json"
	interfaceInjection.enableDependencyInterfaceInjection = false
	runs {
		configureEach {
			property "fabric.log.console.level", "debug"
		}
		client {
			programArg "--username=Dev"
			vmArg "-Xmx7G"
		}
		server {
			vmArg "-Xmx3G"
		}
	}
	generatedIntermediateMappings()
}

dependencies {
	// Uncomment the following line for Loom's default logging output.
	//implementation "org.apache.logging.log4j:log4j-core:${log4j_version}"

	minecraft "com.mojang:minecraft:1.7.10"
	// While it is possible to use stable-12 mappings here, it is not recommended as most mods depends on Forge default mappings in a development environment.
	mappings "net.minecraftforge:forge:${forge_version}:userdev"
	legacyForge "net.minecraftforge:forge:${forge_version}:universal"

	// Uncomment the following lines for mixins.
	//implementation("com.github.LegacyModdingMC.UniMixins:unimixins-all-1.7.10:${unimixins_version}") {transitive = false}
	//annotationProcessor("com.github.LegacyModdingMC.UniMixins:unimixins-all-1.7.10:${unimixins_version}") {transitive = false}

	// Example dependencies
	modImplementation("com.github.GTNewHorizons:CodeChickenLib:${ccl_version}") {transitive = false}
	modImplementation("com.github.GTNewHorizons:CodeChickenCore:${ccc_version}") {transitive = false}
	modImplementation("com.github.GTNewHorizons:NotEnoughItems:${nei_version}") {transitive = false}
}

def replacements = [
	"0@VERSION@": version
]

blossom {
	replaceToken(replacements)
}

processResources {
	inputs.properties(replacements)
	filesMatching(["mcmod.info", "META-INF/mods.toml", "META-INF/neoforge.mods.toml"]) {
		filter {
			it.replace(replacements)
		}
	}
}

def manifestAttributes = [
	"Specification-Title": mod_name,
	"Specification-Vendor": mod_author,
	"Specification-Version": mod_spec_version,
	"Implementation-Title": archivesBaseName,
	"Implementation-Version": mod_version,
	"Implementation-Vendor": mod_author,

	// Uncomment the following lines for early mixin json configs.
	//"TweakClass": "org.spongepowered.asm.launch.MixinTweaker",
	//"ForceLoadAsMod": "true",
]

jar {
	manifest.attributes(manifestAttributes)
}

sourcesJar {
	manifest.attributes(manifestAttributes)
}

tasks.withType(net.fabricmc.loom.task.RunGameTask).configureEach {
	javaLauncher.set(javaToolchains.launcherFor(java.toolchain))
}

// Please refer to Mod Publish Plugin's documentation on how to use the plugin to upload projects to CurseForge/Modrinth.
publishMods {
	file = remapJar.archiveFile
	version = displayName = "1.7.10-${mod_version}"
	changelog = file("changelog.md").text
	type = ALPHA
	modLoaders.add("forge")
	additionalFiles.from(sourcesJar.archiveFile)
}
